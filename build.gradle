buildscript {
    ext.kotlin_version = '1.6.21'
    ext.ktor_version = '1.6.8'
    ext.mockk_version = '1.10.6'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.21" apply false
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

subprojects {
    if (name == "applications" || name == "components") return

    group "io.collective"

    apply plugin: 'kotlin'

    defaultTasks "clean", "build"

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation "io.ktor:ktor-server-core:$ktor_version"
        implementation "io.ktor:ktor-client-core:$ktor_version"
        implementation "io.ktor:ktor-client-java:$ktor_version"
        implementation "io.ktor:ktor-server-netty:$ktor_version"
        implementation "io.ktor:ktor-html-builder:$ktor_version"
        implementation "io.ktor:ktor-jackson:$ktor_version"
        implementation "io.ktor:ktor-client-jackson:$ktor_version"

        implementation "com.rabbitmq:amqp-client:5.14.2"

        implementation "ch.qos.logback:logback-classic:1.2.3"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.4"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.4"

        implementation "io.dropwizard.metrics:metrics-servlet:4.1.5"
        implementation "io.dropwizard.metrics:metrics-core:4.1.5"

        implementation "io.prometheus:simpleclient_dropwizard:0.8.1"
        implementation "io.prometheus:simpleclient_servlet:0.8.1"

        testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
        testImplementation "io.ktor:ktor-client-mock:$ktor_version"
        testImplementation group: 'junit', name: 'junit', version: '4.12'
        testImplementation "io.mockk:mockk:$mockk_version"
        testImplementation "org.awaitility:awaitility:4.0.3"
        testImplementation("org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version")
    }

    sourceSets {
        main.kotlin.srcDir "src/main/kotlin"
        main.resources.srcDir "src/main/resources"

        test.kotlin.srcDir "src/test/kotlin"
        test.resources.srcDir "src/test/resources"
    }

    tasks.withType(Copy).all {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }


    // needed for heroku
    task stage(dependsOn: ['build', 'clean'])
    build { mustRunAfter "clean" }
    gradle.taskGraph.whenReady {
        taskGraph ->
            if (taskGraph.hasTask(stage)) {
                test.enabled = false
            }
    }
}